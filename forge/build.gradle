plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "me.shedaniel.unified-publishing" version "0.1.+"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    runConfigs.configureEach {
        // Use MC_USERNAME and MC_ACCESS_TOKEN in your system environment variables to use your Minecraft account in dev
        if (name == "client") {
            if (System.getenv('MC_USERNAME') && System.getenv('MC_UUID') && System.getenv('MC_ACCESS_TOKEN')) {
                programArgs '--username', System.getenv('MC_USERNAME'), '--uuid', System.getenv('MC_UUID'), '--accessToken', System.getenv('MC_ACCESS_TOKEN')
            }
        } else if (name == "data") {
            programArgs '--existing', project(":common").file("src/main/resources/").absolutePath
        }
    }

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "vc_gliders-common.mixins.json"
        mixinConfig "vc_gliders.mixins.json"

        dataGen {
            mod "vc_gliders"
        }
    }
}

unifiedPublishing {
    project {
        displayName = "üçÇ [Forge] - Gliders - $rootProject.minecraft_version-v$rootProject.mod_version"
        releaseType = "release"
        changelog = new File("${rootProject.projectDir}/changelog.html").text
        gameVersions = ["1.20.1"]
        mainPublication tasks.remapJar
        gameLoaders = ["forge"]
        relations {

        }

        var CURSE_API_KEY = project.findProperty("curseforge") ?: System.getenv("curseforge") ?: ""
        if (CURSE_API_KEY != "") {
            curseforge {
                token = CURSE_API_KEY
                id = "828331"
                gameVersions.addAll "Java 17", "1.20.1"
            }
        }

        var MODRINTH_TOKEN = project.findProperty("modrinth") ?: System.getenv("modrinth") ?: ""
        if (MODRINTH_TOKEN != "") {
            modrinth {
                token = MODRINTH_TOKEN
                id = "XiC6HzoU"
                version = "$rootProject.mod_version+$project.name"
            }
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    modApi "net.threetag:PalladiumCore-forge:${rootProject.palladiumcore_version}"
    include "net.threetag:PalladiumCore-forge:${rootProject.palladiumcore_version}"

    // Player Animator
    include modImplementation("curse.maven:playeranimator-658587:4587214")
  //  include modImplementation("dev.kosmx.player-anim:player-animation-lib-forge:${rootProject.player_anim_version}")

    // JEI
    modCompileOnly "mezz.jei:jei-${rootProject.minecraft_version}-forge-api:${rootProject.jei_version}"

    // REI
    modImplementation "me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}"

    // Configured & Catalogue
    modLocalRuntime "curse.maven:catalogue-459701:${rootProject.catalogue_version}"

    // Curios
    modLocalRuntime "top.theillusivec4.curios:curios-forge:${rootProject.curios_version}"
    modCompileOnly "top.theillusivec4.curios:curios-forge:${rootProject.curios_version}:api"


    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
    inputs.property "mod_version", rootProject.mod_version
    inputs.property "mod_name", rootProject.mod_name
    inputs.property "mod_author", rootProject.mod_author
    inputs.property "mod_license", rootProject.mod_license

    filesMatching("META-INF/mods.toml") {
        expand "mod_version": rootProject.mod_version,
                "mod_name": rootProject.mod_name,
                "mod_author": rootProject.mod_author,
                "mod_license": rootProject.mod_license
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
    archiveClassifier = 'forge-' + rootProject.mod_version
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
